dnl Process this file with autoconf to produce a configure script.

AC_INIT
AC_CONFIG_SRCDIR([Include/bicpl.h])
AC_PREREQ(2.57)
AM_INIT_AUTOMAKE(bicpl, 1.4.5)
AC_CONFIG_HEADERS([config.h])

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

dnl Build only static libs by default
AM_DISABLE_SHARED
AM_PROG_LIBTOOL

dnl Add extra include or link directories
smr_WITH_BUILD_PATH

mni_REQUIRE_VOLUMEIO

AC_FUNC_FORK
AC_CHECK_FUNCS(srandom random cbrt gamma gettimeofday)
AC_CHECK_HEADERS([sys/time.h])

dnl Decide which file format is used for images.  The installer *must* choose
dnl a "--with-image-X" option, otherwise no image I/O is possible

imagelib=none

AC_ARG_WITH([image-netpbm],
  [  --with-image-netpbm     use netpbm library to output raster data],
  [imagelib=netpbm]
)

AC_ARG_WITH([image-ppm],
  [  --with-image-ppm        use PPM library to output raster data],
  [imagelib=ppm]
)

AC_ARG_WITH([image-sgi],
  [  --with-image-sgi        use SGI image library to output raster data],
  [imagelib=sgi]
)

case "$imagelib" in
    none)
	AC_MSG_WARN([No image library chosen.  Raster output disabled!!])
	imageio_GLUE_CODE=rgb_io_none.c
	;;
    netpbm)
	smr_REQUIRED_LIB(netpbm,ppm_writeppminit,ppm.h,[-lnetpbm])
 	imageio_GLUE_CODE=rgb_io_ppm.c
	;;
    ppm)
	smr_REQUIRED_LIB(ppm,ppm_writeppminit,ppm.h,[-lpgm -lpbm])
 	imageio_GLUE_CODE=rgb_io_ppm.c
	;;
    sgi)
	smr_REQUIRED_LIB(image,iopen,image.h)
	imageio_GLUE_CODE=rgb_io_sgi.c
	;;
    *)
	AC_MSG_ERROR([configure is broken.  Fix me.])
	;;
esac

AC_DEFINE_UNQUOTED(imageio_GLUE_CODE,
    ["$imageio_GLUE_CODE"],[File to include in Images/rgb_io.c])


AC_CONFIG_FILES([Makefile
Data_structures/Makefile
Documentation/Makefile Documentation/doxygen.config
Geometry/Makefile
Images/Makefile
Include/Makefile Include/bicpl/Makefile
Marching_cubes/Makefile
Numerical/Makefile
Objects/Makefile
Prog_utils/Makefile
Testing/Makefile
Transforms/Makefile
Volumes/Makefile
tools/Makefile
Deform/Makefile
bicpl_clapack/Makefile
])
AC_OUTPUT([epm-header])
